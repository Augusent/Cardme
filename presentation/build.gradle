apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.firebase-crash'
apply plugin: 'com.google.firebase.firebase-perf'

apply from: "../buildsystem/script-git-version.gradle"

def config = rootProject.ext.configurations
def signIn = rootProject.ext.signIn

android {
    compileSdkVersion config.compileSdkVersion

    defaultConfig {
        applicationId "com.popalay.cardme"
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion

        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
    }

    signingConfigs {
        debug {
            storeFile file(signIn.debugStoreFile)
        }

        release {
            storeFile file(signIn.releaseStoreFile)
            keyAlias signIn.keyAlias
            keyPassword signIn.keyPassword
            storePassword signIn.storePassword
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            debuggable true
        }

        release {
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "ignored"

    productFlavors {

        dev {
            versionCode gitVersionCodeTime
            versionName gitVersionName
        }

        prod {
            versionCode gitVersionCode
            versionName gitVersionName
        }
    }

    dataBinding.enabled true

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

kapt {
    correctErrorTypes = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':data')
    api project(":domain")

    implementation support.cardView
    implementation support.recyclerView
    implementation support.supportLib
    implementation support.design
    implementation support.customTabs
    implementation support.constraintLayout

    implementation arch.archComponentsRuntime
    implementation arch.archComponentsExtensions
    kapt arch.archComponentsCompiler

    implementation lsjwzh.recyclerViewPager
    implementation amulyakhare.textDrawable

    implementation lastadapter.lastAdapter

    kapt databinding.compiler
    implementation rxDatabindings.rxdatabindings

    implementation terrakok.cicerone

    implementation dagger.dagger
    kapt dagger.daggerCompiler
    implementation dagger.daggerAndroidSupport
    kapt dagger.daggerAndroidProcessor

    implementation rxJava2.rxJava
    implementation rxJava2.rxAndroid
    implementation rxJava2.rxKotlin
    implementation jakewharton.rxRelay

    implementation cardIo.android

    implementation matthiasrobbers.shortbread
    kapt matthiasrobbers.shortbreadCompiler

    implementation firebase.firebaseCore
    implementation firebase.firebasePerf
    implementation firebase.firebaseCrash

    implementation uber.autoDispose
    implementation uber.autoDisposeKotlin
    implementation uber.autoDisposeArchcomponents
    implementation uber.autoDisposeArchcomponentsKotlin

    implementation development.androidProguards
    implementation development.stetho
    debugImplementation development.debugDb
    debugImplementation development.leakCanary
    releaseImplementation development.leakCanaryNoOp
    testImplementation development.leakCanaryNoOp
}

apply plugin: 'com.google.gms.google-services'

afterEvaluate {
    android.sourceSets.all { sourceSet ->
        if (!sourceSet.name.startsWith("test")) {
            sourceSet.kotlin.setSrcDirs([])
        }
    }
}