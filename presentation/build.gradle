apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.firebase-crash'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'realm-android'

apply from: "$project.rootDir/buildsystem/script-git-version.gradle"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.popalay.cardme"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
    }

    signingConfigs {
        debug {
            storeFile file("../buildsystem/debug.keystore")
        }

        release {
            storeFile file("../buildsystem/release.keystore")
            keyAlias "yocard"
            keyPassword "wr7qUc2e6e"
            storePassword "wr7qUc2e6e"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            debuggable true
        }

        release {
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "ignored"

    productFlavors {

        all {
            buildConfigField "long", "DATABASE_VERSION", "3L"
        }

        dev {
            versionCode gitVersionCodeTime
            versionName gitVersionName
        }

        prod {
            versionCode gitVersionCode
            versionName gitVersionName
        }
    }

    dataBinding.enabled true

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(":data")
    implementation project(":domain")

    implementation presentationDependencies.cardView
    implementation presentationDependencies.recyclerView
    implementation presentationDependencies.supportLib
    implementation presentationDependencies.supportDesign
    implementation presentationDependencies.customTabs
    implementation presentationDependencies.constraintLayout

    kapt presentationDependencies.databindingCompiler
    implementation presentationDependencies.rxDatabinding

    implementation presentationDependencies.archComponentsRuntime
    implementation presentationDependencies.archComponentsExtensions
    kapt presentationDependencies.archComponentsCompiler

    implementation presentationDependencies.router

    implementation presentationDependencies.dagger
    kapt presentationDependencies.daggerCompiler
    implementation presentationDependencies.daggerAndroidSupport
    kapt presentationDependencies.daggerAndroidProcessor

    implementation presentationDependencies.rxJava
    implementation presentationDependencies.rxAndroid
    implementation presentationDependencies.rxKotlin
    implementation presentationDependencies.rxRelay
    implementation presentationDependencies.rxRealm

    implementation presentationDependencies.cardIo
    implementation presentationDependencies.gson

    implementation presentationDependencies.lastAdapter

    implementation developmentDependencies.stetho
    implementation developmentDependencies.stethoRealm
    implementation developmentDependencies.androidProguards
    debugImplementation developmentDependencies.leakCanary
    releaseImplementation developmentDependencies.leakCanaryNoOp
    testImplementation developmentDependencies.leakCanaryNoOp

    implementation presentationDependencies.streamApi
    implementation presentationDependencies.contacts
    implementation presentationDependencies.androidPermissionX

    implementation presentationDependencies.shortbread
    kapt presentationDependencies.shortbreadCompiler

    implementation presentationDependencies.recyclerViewPager
    implementation presentationDependencies.textDrawable

    implementation presentationDependencies.firebaseCore
    implementation presentationDependencies.firebasePerf
    implementation presentationDependencies.firebaseCrash
}

apply plugin: 'com.google.gms.google-services'

afterEvaluate {
    android.sourceSets.all { sourceSet ->
        if (!sourceSet.name.startsWith("test")) {
            sourceSet.kotlin.setSrcDirs([])
        }
    }
}