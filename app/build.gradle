apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-crash'
apply plugin: 'realm-android'
apply from: "$project.rootDir/tools/script-git-version.gradle"

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "com.popalay.cardme"
        minSdkVersion 21
        targetSdkVersion 25
    }

    signingConfigs {
        debug {
            storeFile file("keys/debug.keystore")
        }

        release {
            storeFile file("keys/release.keystore")
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            debuggable true
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "ignored"

    productFlavors {
        dev {
            versionCode gitVersionCodeTime
            versionName gitVersionName
        }

        prod {
            versionCode gitVersionCode
            versionName gitVersionName
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding.enabled true
}

ext {
    supportLibrary = '25.3.1'
    playServices = '10.2.6'
    dagger = '2.9'
    moxy = '1.5.2'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Support
    implementation "com.android.support:cardview-v7:$supportLibrary"
    implementation "com.android.support:recyclerview-v7:$supportLibrary"
    implementation "com.android.support:support-v4:$supportLibrary"
    implementation "com.android.support:design:$supportLibrary"
    implementation "com.android.support:customtabs:$supportLibrary"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // MVP
    implementation "com.arello-mobile:moxy-app-compat:$moxy"
    annotationProcessor "com.arello-mobile:moxy-compiler:$moxy"
    implementation 'com.github.popalay:rx-lifecycle-moxy:1.0.0'

    // DI
    implementation "com.google.dagger:dagger:$dagger"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger"

    // Rx
    implementation 'io.reactivex:rxjava:1.3.0'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'com.trello:rxlifecycle:1.0'

    // DB
    implementation 'com.github.popalay:rx-realm:1.0.5'

    // Utils
    implementation 'io.card:android-sdk:5.5.1'
    implementation 'com.github.nitrico.lastadapter:lastadapter:2.2.2'
    implementation 'com.facebook.stetho:stetho:1.4.1'
    implementation 'com.uphyca:stetho_realm:2.0.0'
    implementation 'com.annimon:stream:1.1.6'
    implementation 'com.github.tamir7.contacts:contacts:1.1.4'
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'com.github.Popalay:AndroidPermissionX:0142673ad1'

    // Shortcuts
    implementation 'com.github.matthiasrobbers:shortbread:1.0.1'
    annotationProcessor 'com.github.matthiasrobbers:shortbread-compiler:1.0.1'

    // Widgets
    //compile 'com.github.Popalay:LandscapeView:master-fbcd6c9a61-1'
    implementation 'com.github.lsjwzh.RecyclerViewPager:lib:v1.1.2'

    // Firebase
    implementation "com.google.firebase:firebase-core:$playServices"
    implementation "com.google.firebase:firebase-crash:$playServices"

    // Proguard rules
    implementation 'com.github.yongjhih.android-proguards:android-proguards:2.0.0'

    testImplementation 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'